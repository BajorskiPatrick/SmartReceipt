plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.7'
	id 'io.spring.dependency-management' version '1.1.7'
    id "org.openapi.generator" version "7.4.0"
}

group = 'com.sp'
version = '0.0.1-SNAPSHOT'
description = 'SmartReceipt web application with integrated AI module'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-authorization-server'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'

    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.13'
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.20'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'

    compileOnly 'org.projectlombok:lombok'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.register('generateServer', org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    generatorName = "spring"
    inputSpec = "$rootDir/src/main/resources/api/openapi.yaml"
    outputDir = "$buildDir/generated/server"
    apiPackage = ""
    modelPackage = "com.sp.smartreceipt.model"

    globalProperties = [
            models: "",
            apis: "false",
            supportingFiles: "false"
    ]

    configOptions = [
            useSpringBoot3: "true",
            useBeanValidation: "true",
            performBeanValidation: "true",
            serializableModel: "true",
            additionalModelTypeAnnotations: "@lombok.Builder; @lombok.Data; @lombok.NoArgsConstructor; @lombok.AllArgsConstructor"
    ]
}

sourceSets {
    main {
        java {
            srcDir "$buildDir/generated/server/src/main/java"
        }
    }
}

compileJava.dependsOn tasks.named('generateServer')

tasks.register('generateClient', org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    generatorName = "typescript-axios"
    inputSpec = file("$rootDir").toPath().resolve("src/main/resources/api/openapi.yaml").toString()
    outputDir = file("$rootDir").parentFile.toPath().resolve("frontend/src/api-client").toString()

    apiPackage = "api"
    modelPackage = "models"

    globalProperties = [
            models: "",
            apis: "false",
            supportingFiles: "false"
    ]

    configOptions = [
            npmName: "@smartreceipt/api-client",
            supportsES6: "true",
            withSeparateModelsAndApi: "true"
    ]
}
