plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
    id "org.openapi.generator" version "7.4.0"
}

group = 'com.sp'
version = '0.0.1-SNAPSHOT'
description = 'Spring Boot and React Billing Application'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(23)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'io.jsonwebtoken:jjwt:0.9.1'
    implementation 'javax.xml.bind:jaxb-api:2.3.1'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.13'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    compileOnly 'org.projectlombok:lombok:1.18.36'
    runtimeOnly 'org.postgresql:postgresql'

    annotationProcessor 'org.projectlombok:lombok:1.18.36'
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.register('generateServer', org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    generatorName = "spring"
    inputSpec = "$rootDir/src/main/resources/api/openapi.yaml"
    outputDir = "$buildDir/generated/server"
    apiPackage = ""
    modelPackage = "com.sp.smartreceipt.model"

    globalProperties = [
            models: "",
            apis: "false",
            supportingFiles: "false"
    ]

    configOptions = [
            useSpringBoot3: "true",
            useBeanValidation: "true",
            performBeanValidation: "true",
            additionalModelTypeAnnotations: "@lombok.Builder; @lombok.Data; @lombok.NoArgsConstructor; @lombok.AllArgsConstructor"
    ]

    typeMappings = [
            Double: "java.math.BigDecimal",
            double: "java.math.BigDecimal",
            Float:  "java.math.BigDecimal",
            float:  "java.math.BigDecimal",
            number: "java.math.BigDecimal"
    ]

    importMappings = [
            BigDecimal: "java.math.BigDecimal"
    ]
}

sourceSets {
    main {
        java {
            srcDir "$buildDir/generated/server/src/main/java"
        }
    }
}

compileJava.dependsOn tasks.named('generateServer')

tasks.register('generateFrontendClient', org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    generatorName = "typescript-axios"
    inputSpec = file("$rootDir").toPath().resolve("src/main/resources/api/openapi.yaml").toString()
    outputDir = file("$rootDir").parentFile.toPath().resolve("frontend/src/api-client").toString()

    apiPackage = "api"
    modelPackage = "models"

    globalProperties = [
            models: "",
            apis: "MainApp",
            supportingFiles: "index.ts,base.ts,configuration.ts,common.ts,api.ts"
    ]

    configOptions = [
            npmName: "@smartreceipt/api-client",
            supportsES6: "true",
            withSeparateModelsAndApi: "true",
            stringEnums: "true"
    ]
}

tasks.register('generateAiClient', org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    generatorName = "python-fastapi"
    inputSpec = file("$rootDir").toPath().resolve("src/main/resources/api/openapi.yaml").toString()
    outputDir = file("$rootDir").parentFile.toPath().resolve("ai-module/src/api-client").toString()

    globalProperties = [
            apis: "OcrModule",
            models: ""
    ]

    configOptions = [
            packageName: "app",
            projectName: "smartreceipt-ai",
            serverPort: "8000"
    ]
}
